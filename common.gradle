plugins {
	id "maven-publish"
	id "fabric-loom" version "1.4-SNAPSHOT"
	id "com.replaymod.preprocess" version "ce1aeb2b"
	id 'me.modmuss50.mod-publish-plugin' version '0.3.4'
}

int mcVersion = 1
preprocess {
	mcVersion = vars.get()["MC"]
	tabIndentation = true
}

repositories {
	mavenCentral()
	maven {
		url "https://masa.dy.fi/maven"
	}
	maven {
		url "https://maven.terraformersmc.com/releases/"
	}
	maven {
		url "https://jitpack.io"
	}
	maven {
		url "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
	}
	maven {
		url 'https://www.cursemaven.com'
	}
}

archivesBaseName = project.archives_base_name
version = project.minecraft_version + "-" + project.mod_version
group = project.maven_group

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "carpet:fabric-carpet:${project.carpet_core_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	// Required for Arucas
	include implementation("org.jetbrains.kotlin:kotlin-stdlib:1.7.10")
	include implementation("com.github.senseiwells:Arucas:${project.arucas_version.length() > 10 ? project.arucas_version.substring(0, 10) : project.arucas_version}")

	// include "net.dv8tion:JDA:5.0.0-alpha.19"
	// include implementation("com.github.senseiwells:ArucasDiscordAPI:${project.arucas_discord_api_version.length() > 10 ? project.arucas_discord_api_version.substring(0, 10) : project.arucas_discord_api_version}")

	// Runtime Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	// if (project.minecraft_version in ["1.16.5", "1.17.1", "1.18.2"]) {
	//     modImplementation fabricApi.module("fabric-textures-v0", project.fabric_version)
	// }
	// modImplementation fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version)

	// Account authentication at runtime
	modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:1.0.0"

	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		def map = [
			"version"             : project.mod_version,
			"carpet_dependency"   : project.carpet_dependency,
			"minecraft_dependency": project.minecraft_dependency
		]
		expand map
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	it.options.release = 16
}

java {
	withSourcesJar()
}

publishMods {
	file = remapJar.archiveFile
	changelog = """
	- Fixed a conflict with ViaFabricPlus
	- Fixed some obfuscation issues with clientscript
	"""
	type = STABLE
	modLoaders.add("fabric")

	displayName = "EssentialClient ${project.minecraft_version} v${project.mod_version}"

	modrinth {
		accessToken = providers.environmentVariable("MODRINTH_API_KEY")
		projectId = "sH0dfrKf"
		minecraftVersions.add(project.minecraft_version)

		requires {
			id = "P7dR8mSH"
		}
		requires {
			id = "TQTTVgYE"
		}
		optional {
			id = "mOgUt4GM"
		}
	}
}


// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(jar) {
				builtBy remapJar
			}
		}
	}
}